### 什麼是單子
* 能夠提供 join 函數型別稱為單子
* 單子額外的限制讓它成為 haskell 中最強大的函子類別
```haskell
join :: [[a]] -> [a]
join = concat
```
* 任一單子都必須適應用函子
```haskell
class Applicative m => Monad m where
  return :: a -> m a
  return = pure
  
  join :: m (m a) -> m a
```
* 型別變數 m 代表要限制的函子型別
* 使用 pure 函數作為 return 函數的預設定義，所以 Applicative 是它的父類別
* join 函數和 return 函數組合後，得到一個非常有用的函數
```haskell
(>>=) :: m a -> (a -> m b) -> m b
x >>= f = join $ fmap f x
```
#### >>=繫結(bind)函數的工作過程： 
* 函數 f 的型別是 f :: a -> m b
* 經過 fmap 升格後，得到 fmap f :: m a -> m (m b)
* 再經過 join 合併掉一層函子，得到 m b 型別的結果
```haskell
import Text.Read
let envPort = Just "8080"
envPort >>= readMaybe :: Maybe Int
-- Just 8080
```
#### 證明單子比應用函子強大的一個例子 (<*> 可以透過 join 實作)
* 改寫自然升格
```haskell
replicate <$> Just 3 <*> Just 'x'
-- Just "xxx"

Just 3 >>= \n ->
  Just 'x' >>= \x -> 
    Just (replicate n x)
-- Just "xxx"
```

* 可以輕鬆解決窮舉法問題 (計算邊長 1 到 10 的所有取矩形)
```haskell
[1..10] >>= \x -> 
  [x..10] >>= \y -> 
    return (x * y)
```

* 應用函子最大限制：無法在計算中更改上下文資訊，必須在計算開始前，由參數決定好最終容器的形狀

#### do 語法糖
* 反箭頭表示從函子包裹取值
* do 中的 let 就是 let...in... 的語法
```haskell
count :: Int
count = sum $ do 
  x <- [1..10]
  y <- [x..10]
  let area = x * y
  return  $ if area < 50 then 1 else 0
-- 把計算出的面積，臨時綁定到 area 上

count = sum $
  [1..10] >>= \x ->
    [x..10] >>= \y ->
      let area = x * y
      in return $
        if area < 50 then 1 else 0
```

#### IO 單子
* 如何使用 do 語法建構 IO 運算
```haskell
getLine :: IO String
putStrLn :: String -> IO()

main :: IO ()
main = getLine >>= putStrLn

main = do 
  line <- getLine
  putStrLn line
```

#### Control.Monad
* 定義了一整套結構控制函數，可以作用在所有單子型別上，不僅線於 IO
```haskell
when :: Applicative f => Bool -> f () -> f ()
when p s = if p then s else pure ()

unless :: Applicative f => Bool -> f () -> f ()
unless p s = if p then pure () else s
```
```haskell
import Control.Monad

main = do 
  n <- readLn
  when (n < 0) $ print "OK"
```
